apply plugin: 'com.android.application'
apply from: rootProject.file('gradle/git_utils.gradle')

def appVersionCode = gitCommitUnixTime()
def appVersionName = "${version}-${gitBranch()}-${gitHash()} (${appVersionCode})"

task printVersion {
    doLast {
        println "${appVersionName}"
    }
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "net.gini.android.vision.screenapiexample"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode appVersionCode
        versionName appVersionName

        // Use the test runner with JUnit4 support
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        sourceSets {
            main {
                java.srcDirs += "$rootProject.projectDir/exampleShared/java"
            }
        }

    }

    signingConfigs {
        release {
            storeFile file(releaseKeystoreFile)
            storePassword releaseKeystorePassword
            keyAlias releaseKeyAlias
            keyPassword releaseKeyPassword
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
}

// For testing a released version
repositories {
    maven {
        url 'https://repo.gini.net/nexus/content/repositories/open'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile deps.appCompatv7

    compile 'com.github.tony19:logback-android-core:1.1.1-5'
    compile 'com.github.tony19:logback-android-classic:1.1.1-5'

    compile('net.gini:gini-android-sdk:1.3.92@aar') {
        transitive = true
    }

    // For testing the local version
    compile project(path: ':ginivision')
    // For testing a released version
    //compile 'net.gini:gini-vision-lib:2.0.0-alpha.1'

    testCompile deps.junit

    androidTestCompile(deps.supportTestRunner, {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestCompile deps.truth
    androidTestCompile(deps.supportTestRules, {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestCompile(deps.supportTestEspressoCore, {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestCompile(deps.supportTestEspressoIntents, {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestCompile deps.supportTestUiAutomator
    androidTestCompile deps.mockito
    androidTestCompile deps.mockitoAndroid
    androidTestCompile deps.supportMultidex
}

apply from: rootProject.file('gradle/codequality.gradle')
apply from: rootProject.file('gradle/client_credentials.gradle')
apply from: rootProject.file('gradle/multidex_for_tests.gradle')