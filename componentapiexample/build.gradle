apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply from: rootProject.file('gradle/git_utils.gradle')
apply from: rootProject.file('gradle/gini_credentials.gradle')

def appVersionCode = gitCommitUnixTime()
def appVersionName = "${version}-${gitBranch()}-${gitHash()} (${appVersionCode})"

task printVersion {
    doLast {
        println "${appVersionName}"
    }
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        applicationId "net.gini.android.vision.componentapiexample"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode appVersionCode
        versionName appVersionName

        sourceSets {
            main {
                java.srcDirs += "$rootProject.projectDir/exampleShared/java"
            }
        }

        multiDexEnabled true
    }

    signingConfigs {
        release {
            storeFile file(releaseKeystoreFile)
            storePassword releaseKeystorePassword
            keyAlias releaseKeyAlias
            keyPassword releaseKeyPassword
        }
    }

    buildTypes {
        debug {
            resValue 'string', 'gini_api_client_id', clientId
            resValue 'string', 'gini_api_client_secret', clientSecret
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            resValue 'string', 'gini_api_client_id', clientId
            resValue 'string', 'gini_api_client_secret', clientSecret
        }
    }

}

// For testing a released version
repositories {
    // Use repo on filesystem for using local builds
    mavenLocal()
    maven {
        url 'https://repo.gini.net/nexus/content/repositories/open'
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation deps.androidxAppCompat
    implementation deps.androidxCoordinatorLayout
    implementation deps.androidxMultidex
    implementation deps.androidxConstraintLayout

    implementation 'com.karumi:dexter:6.0.2'

    implementation 'com.github.tony19:logback-android-core:1.1.1-6'
    implementation('com.github.tony19:logback-android-classic:1.1.1-6') {
        // workaround issue #73
        exclude group: 'com.google.android', module: 'android'
    }

    // For backward compatibility
    implementation('net.gini:gini-android-sdk:2.3.0@aar') {
        transitive = true
    }

    // For testing the local version
    implementation project(path: ':ginivision-network')
    // For testing a released version
    //implementation 'net.gini:gini-vision-network-lib:2.5.0'

    // For testing the local version
    implementation project(path: ':ginivision-accounting-network')
    // For testing a released version
    //implementation 'net.gini:gini-vision-network-lib:2.5.0'

    testImplementation deps.junit

    // For testing the local version
    implementation project(path: ':ginivision')
    // For testing a released version
//    implementation 'net.gini:gini-vision-lib:2.0.0-alpha.1'
}

apply from: rootProject.file('gradle/codequality.gradle')
